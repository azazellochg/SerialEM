# (C) Nexperion e.U. - Solutions for Electron Microscopy 2018
# https://www.nexperion.net
# Last update February 26, 2018
# Requires SerialEM >= 3.6.x

# Parameters not covered in this version of the script include: JEOL Alpha, diffraction/imaging mode, piezo drive, filter, camera settings (inserted/retracted)

ScriptName SaveScopeState
SuppressReports

echo
echo SAVING MICROSCOPE STATE:
IsVariableDefined _N3SFlag
if ( $ReportedValue1 == 1)
  YesNoBox Do you want to overwrite the currently saved microscope state?
  If ( $ReportedValue1 != 1) 
    echo Cancelled.
    echo
    Exit
  EndIf
EndIf

echo Saving illumination ...
ReportProbeMode
_N3SProbeMode := $ReportedValue1
ReportSpotSize
_N3SSpotSize := $ReportedValue1
ReportPercentC2
_N3SPercentC2 := $ReportedValue1
ReportBeamShift
_N3SBeamShiftX := $ReportedValue1
_N3SBeamShiftY := $ReportedValue2
ReportBeamTilt
_N3SBeamTiltX := $ReportedValue1
_N3SBeamTiltY := $ReportedValue2

echo Saving image shift ...
ReportImageShift
_N3SImageShiftX := $ReportedValue1
_N3SImageShiftY := $ReportedValue2

echo Saving focus ...
ReportAbsoluteFocus
_N3SAbsoluteFocus := $ReportedValue1

echo Saving magnification ...
ReportMag
_N3SMag := $ReportedValue1

echo Saving stage position ...
ReportStageXYZ
_N3SStageX := $ReportedValue1
_N3SStageY := $ReportedValue2
_N3SStageZ := $ReportedValue3
ReportTiltAngle
_N3SStageA := $ReportedValue1

_N3SFlag := 1

echo DONE.
echo
