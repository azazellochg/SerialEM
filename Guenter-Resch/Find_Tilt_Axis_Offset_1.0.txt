# (C) Nexperion e.U. - Solutions for Electron Microscopy 2017
# https://www.nexperion.net
# Last update April 8, 2017
# Requires SerialEM >= 3.6.0
# Finds an optimised value for the TiltAxisOffset property (the lateral displacement between optical axis and tilt axis of the stage). The script measures the offset averaging $Samples values, then image shifts to the new values, measures again, until the change is below $ThresholdOffset.

ScriptName FindTiltAxisOffset
SuppressReports

# Parameters
Samples = 3
ThresholdOffset = 0.15

Success = 0

Pause Activate "Center image shift on tilt axis" in the "Image Alignment & Focus" panel and make sure the sample is close to eucentric height.
SetProperty TiltAxisOffset 0.0
AverageAxisOffset = 0.0
echo
echo Starting at 0.0 um offset ($Samples measurements per position) ...

Loop 10
    j = 0
    Loop $Samples
        Eucentricity 2
        ReportLastAxisOffset 
        j = $j + $ReportedValue1
    EndLoop 
    DeltaAxisOffset = ( $j / $Samples )  - $AverageAxisOffset 
    AverageAxisOffset = ( $j / $Samples )
    if ( ABS ( $DeltaAxisOffset ) <= $ThresholdOffset ) 
        Success = 1
        echo Stopping at $DeltaAxisOffset um change
        break
    endif
    echo
    echo Continuing at $AverageAxisOffset um offset (last change $DeltaAxisOffset um) ...
    SetProperty TiltAxisOffset $AverageAxisOffset
EndLoop

if ( $Success == 1 )
    echo Use "TiltAxisOffset $AverageAxisOffset" in your properties file
elseif
    echo The routine did not get below $ThresholdOffset um
endif
FlashDisplay 4 0.15
