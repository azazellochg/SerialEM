# (C) Nexperion e.U. - Solutions for Electron Microscopy 2016 - 2017
# https://www.nexperion.net
# Last update March 15, 2017
# Requires SerialEM >= 3.6.0

ScriptName BeamIntCalTest
SuppressReports 
echo
echo SerialEM beam intensity calibration test script 
echo (C) Nexperion e.U. - Solutions for Electron Microscopy 2016 - 2017

# Settings
BeamIntensityTolerance = .1
MinimumIntensity = 10

Pause Center beam, set up an intense illumination filling the whole field of view and with a valid beam intensity calibration (spot size highlighted in blue). The "Record" parameter set has to be set up not to oversaturate, the condition to stop scripts at low counts has to be switched off (Script > Controls).

# Read current settings
ReportSpotSize 
SpotSize = $ReportedValue1
ReportProbeMode
if ( $ReportedValue1 == 0 )
  ProbeModeText = nP
else
  ProbeModeText = uP
endif

echo 
echo Testing beam intensity calibration for spot size $SpotSize $ProbeModeText ...
ReportPercentC2
SavePercentC2 = $ReportedValue1
Record
ReportMeanCounts 
NewIntensityForMean = $ReportedValue1
Loop 100
  NewIntensityForMean = ( $NewIntensityForMean * 0.57 )
  if ( $NewIntensityForMean < $MinimumIntensity )
    echo Ending at $NewIntensityForMean counts.
    Break
  endif
  ReportPercentC2 
  PreSetPercentC2 = $ReportedValue1
  SetIntensityForMean $NewIntensityForMean
  ReportPercentC2 
  # Catch "Warning: attempting to set beam strength beyond calibrated range"
  if ( $ReportedValue1 == $PreSetPercentC2 ) 
    echo Quitting at $NewIntensityForMean counts (target counts were not applied) ...
    Break 
  Endif
  Record
  ReportMeanCounts 
  MeanRelTar = $ReportedValue1 / $NewIntensityForMean
  if ( $MeanRelTar > ( 1 - $BeamIntensityTolerance) ) and ( $MeanRelTar < ( 1 + $BeamIntensityTolerance) )
    echo > Beam intensity calibration test OK ($MeanRelTar of target)
  else
    echo > Beam intensity calibration test FAILED ($MeanRelTar of target)
  EndIf
EndLoop 

# Restore Illumination
SetPercentC2 $SavePercentC2
Record
CenterBeamFromImage 

echo 
FlashDisplay 4 0.15
echo Done,

