Function BlackStripeCheckK3

# Function to detect black stripes in K3 images, will pause script, close valves and send email.
# Call this function anywhere in a script, with last image in buffer a.
# 2019 Wim Hagen, EMBL Heidelberg

echo BlackStripeCheckK3

SuppressReports

# EMBL k3 black stripe mean ~2.7, image with valves closed ~17 in bin 4 images
BlackStripeTreshold = 5

   # calculate stripe size
   ImageProperties A xsize ysize binning
   StripSize = 576 / $binning

   # 90 degree rotation so horizontal stripes
   If $xsize < $ysize
      Loop 10 index
         x0 = 0
         x1 = $xsize - 1
         y0 = ($index - 1) * $StripSize + 10    # for a litle bit of safety margin
         y1 = $index * $StripSize - 10            # for a litle bit of safety margin
         SubareaMean $x0 $x1 $y0 $y1 mean
         If $mean < $BlackStripeTreshold 
            echo  mean =  $mean : black stripe in sector $index
            MailSubject Krios 2
            SendEmail Black stripe problem Krios 2 K3, acquisition paused, valves closed.
            #SetColumnOrGunValve 1
            Pause See EM-help/Problems/Large black stripe, then continue.
         endif
      endloop
   Endif 

   # 0 degree rotation so vertical stripes
   If $xsize > $ysize
      Loop 10 index
         echo $index
         x0 = ($index - 1) * $StripSize + 10    # for a litle bit of safety margin
         x1 = $index * $StripSize - 10            # for a litle bit of safety margin
         y0 = 0
         y1 = $ysize - 1 
         SubareaMean $x0 $x1 $y0 $y1 mean
         If $mean < $BlackStripeTreshold 
            echo  mean =  $mean : black stripe in sector $index
            MailSubject Krios 2
            SendEmail Black stripe problem Krios 2 K3, acquisition paused, valves closed.
            #SetColumnOrGunValve 1
            Pause See EM-help/Problems/Large black stripe, then continue.
         endif
      endloop
   Endif 

endfunction