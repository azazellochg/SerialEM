MacroName GroupedDoseSymmetricTiltScheme

# Less conservative dose-symmetric tilt scheme for cellular tomography 
# Script does not measure drift! It only waits after tilt, autofocus, image, track-after.
# EMBL Heidelberg 2016, Wim J. H.Hagen
#
# Images:
# Track plus in buffer	K
# Track min in buffer L
# Record plus in buffer M
# Record min in buffer N

# SerialEM settings:
# Roll buffers A -> H.
# Setup LowDose
# Set eucentric height.
# Open file.
# Run script.

########## SETTINGS ########## 

step 		= 1	# tilt step
maxtilt 	= 60	# maximum +/- tilt angle
group 		= 4	# number of tilt steps per group
Tiltbacklash    = -1	# negative tilts can be backlashed by this
wait            = 3     # wait after tilt in sec 

########## END SETTINGS ########## 

branchesteps = $maxtilt / $group

plustilt = 0
mintilt = 0

# zero tilt image
CallFunction TiltZero

# loop back and forth in groups
loop $branchesteps
	loop $group
	        plustilt = $plustilt + $step
	        CallFunction TiltPlus
	endloop	
	loop $group
	        mintilt = $mintilt - $step
                CallFunction TiltMinus
	endloop
endLoop

# finish
TiltTo 0
SetDefocus 0
ResetImageShift
CloseFile
echo xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

function TiltZero
	ReportStageXYZ	
	StageX = $ReportedValue1
	StageY = $ReportedValue2

	# track
        CallFunction CheckAutoFill
	T
	Copy A K
	Copy A L

	# Autofocus
        CallFunction CheckAutoFill
	G

	ReportDefocus
	focusplus = $RepVal1
	focusmin = $RepVal1

	# Record
        CallFunction CheckAutoFill
	R
	S
	Copy A M
	Copy A N

	ReportImageShift
	ISxplus = $RepVal1
	ISyplus = $RepVal2
	ISxminus = $RepVal1
	ISyminus = $RepVal2

	# Tracking after
        CallFunction CheckAutoFill
	T
	Copy A K
	Copy A L
endfunction

function TiltPlus
	TiltTo $plustilt
	MoveStageTo $StageX $StageY

	# set to previous used shift/focus
	GoToLowDoseArea R
	SetDefocus $focusplus
	SetImageShift  $ISxplus $ISyplus

        delay $wait sec

	# Track
        CallFunction CheckAutoFill
	T
	AlignTo K

	# Autofocus
        CallFunction CheckAutoFill
        G
	ReportDefocus
	focusplus =  $RepVal1

	# Record
        CallFunction CheckAutoFill
	R
	S
	AlignTo M
	Copy A M

	ReportImageShift
	ISxplus = $RepVal1
	ISyplus = $RepVal2

	# Tracking after
        CallFunction CheckAutoFill
    	T
	Copy A K
endfunction

Function TiltMinus
	TiltTo $mintilt
	TiltBy $Tiltbacklash
	TiltTo $mintilt
	MoveStageTo $StageX $StageY

	# set to previous used shift/focus	
	GoToLowDoseArea R
	SetDefocus $focusmin
	SetImageShift  $ISxminus $ISyminus

        delay $wait sec
	
	# Track
        CallFunction CheckAutoFill
	T
	AlignTo L

	# Autofocus
        CallFunction CheckAutoFill
	G                 
	ReportDefocus
	focusmin = $RepVal1

	# Record
        CallFunction CheckAutoFill
	R
	S
	AlignTo N
	Copy A N

	ReportImageShift
	ISxminus = $RepVal1
	ISyminus = $RepVal2

	# Tracking after
        CallFunction CheckAutoFill
	T
	Copy A L
endfunction

Function CheckAutoFill
loop 10
	AreDewarsFilling
	if $reportedvalue1 == 0
		break
	elseif $reportedvalue1 == 1
                echo dewars are filling
		delay 60 sec
	endif
endloop
endfunction

