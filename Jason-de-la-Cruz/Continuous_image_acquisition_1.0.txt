ScriptName recMov
#20170608 recMov M. Jason de la Cruz

# Copyright (c) 2017, Howard Hughes Medical Institute
#
# Copying and distribution of this file, with or without modification,
# are permitted in any medium without royalty provided the copyright
# notice and this notice are preserved.  This file is offered as-is,
# without any warranty.

# INSTRUCTIONS: Open file to save autodoc metadata (with .mdoc or .idoc).
#               Modify settings in section below.  Start goniometer rotation
#               (outside of SerialEM) at a few degrees past intended start
#               angle, then start this script to initiate continuous recording.
#               When done, close file.
#
# recordMode options: V (View mode), F (Focus mode), R (Record mode)
# frameExposureTime is in seconds

#================================== SETTINGS ===================================
recordMode = R
frameExposureTime = 4
numberFrames = 20
#===============================================================================
echo ===> Running recMov ...

ScreenUp
SetColumnOrGunValve 1
SetBinning $recordMode 1
CameraProperties
AddToAutodoc PhysicalPixel $reportedValue4
ReportCameraLength
AddToAutodoc CameraLength $reportedValue1
SetContinuous $recordMode 1
WriteAutodoc
SetExposure $recordMode $frameExposureTime 0

UseContinuousFrames 1

Loop $numberFrames index
echo === START PROCESS FOR EXPOSURE $index of $numberFrames ===
echo --- Resetting clock
resetclock
echo --- Recording $frameExposureTime -second exposure
$recordMode
WaitForNextFrame
echo --- Saving exposure $index with initial angle
S
reportclock
echo ===
EndLoop

StopContinuous
echo Acquisition complete
RestoreCameraSet $recordMode
echo ===> Scope standby
SetColumnOrGunValve 0
